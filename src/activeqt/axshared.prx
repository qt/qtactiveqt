# This modules provides the header files, installed under "ActiveQt/".
# It also contains shared code that is used by axcontainer and axserver.

TARGET = QtAxBase
QMAKE_DOCS = $$PWD/doc/activeqt.qdocconf
MODULE_INCNAME = ActiveQt
CONFIG += static
QT += gui-private

LIBS    += -lole32 -loleaut32
!wince*:LIBS    += -luser32 -lgdi32 -ladvapi32
win32-g++*:LIBS += -luuid

HEADERS = \
    shared/qaxtypefunctions.h \
    shared/qaxutils_p.h

SOURCES = \
    shared/qaxtypefunctions.cpp \
    shared/qaxutils.cpp

MODULE = axbase
load(qt_module)
# QtActiveQt is too inconsistent with other Qt modules
# to use the generic cmake config files generator
CONFIG -= create_cmake
